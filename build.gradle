plugins {
    id "io.spring.dependency-management" version "1.0.7.RELEASE"
    id 'net.researchgate.release' version '2.8.0'
    id "com.jfrog.bintray" version "1.8.4"
    id 'java'
    id 'jacoco'
}

apply from: 'build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/release-commons.gradle'

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "jira"
    pluginsDir = "$buildDir/plugins"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://m2proxy.atlassian.com/repository/public" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }


}

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'EPAM Report portal. Jira Integration Plugin'

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api:5.0.0-BETA-3'
        annotationProcessor 'com.epam.reportportal:plugin-api:5.0.0-BETA-3'
        compile 'com.epam.reportportal:commons-model:5.0.0-BETA-3'

    } else {
        implementation 'com.github.reportportal:plugin-api:30f47d4'
        annotationProcessor 'com.github.reportportal:plugin-api:30f47d4'
        compile 'com.github.reportportal:commons-model:7ffbf4e'
    }

    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'

    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile 'org.jasypt:jasypt:1.9.2'

    compile('com.atlassian.jira:jira-rest-java-client-core:5.1.2-2bd0a62e')
    compile('io.atlassian.fugue:fugue:4.7.2')

    compile 'net.oauth.core:oauth:20090617'
    compile 'net.oauth.core:oauth-httpclient4:20090913'

    compile 'org.apache.tika:tika-core:1.14'

    testCompile 'org.springframework:spring-test:5.0.9.RELEASE'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

test {
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

build.dependsOn jacocoTestReport

wrapper {
    gradleVersion = '5.4.1'
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Andrei Varabyeu",
                "Plugin-Class": "com.epam.reportportal.extension.bugtracking.jira.JiraStrategyPlugin",
                "Plugin-Service": "api"
//                "Plugin-Dependencies": ""
        )
    }
}

task plugin(type: Jar) {
    baseName = "plugin-${pluginID}"
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

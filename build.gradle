plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id 'net.researchgate.release' version '2.8.1'
    id "com.jfrog.bintray" version "1.8.4"
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/build-quality.gradle'
apply from: 'https://raw.githubusercontent.com/reportportal/gradle-scripts/master/release-fat.gradle'

project.ext {
    releaseMode = project.hasProperty("releaseMode")
    pluginID = "jira"
    pluginsDir = "$buildDir/plugins"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://m2proxy.atlassian.com/repository/public" }

    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }


}

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'EPAM Report portal. Jira Integration Plugin'

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:plugin-api:5.0.0-RC-2'
        annotationProcessor 'com.epam.reportportal:plugin-api:5.0.0-RC-2'
        compile 'com.epam.reportportal:commons-model:5.0.0-RC-1'
    } else {
        implementation 'com.github.reportportal:plugin-api:40d7f90'
        annotationProcessor 'com.github.reportportal:plugin-api:40d7f90'
        compile 'com.github.reportportal:commons-model:ac99b1a'
    }

    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile 'io.atlassian.fugue:fugue:4.7.2'
    compile('com.atlassian.jira:jira-rest-java-client-core:5.1.6')

    compile 'net.oauth.core:oauth:20090617'
    compile 'net.oauth.core:oauth-httpclient4:20090913'

    compile 'org.apache.tika:tika-core:1.14'

    testCompile 'org.springframework:spring-test:5.2.1.RELEASE'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

test {
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

build.dependsOn jacocoTestReport

wrapper {
    gradleVersion = '5.4.1'
}

jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginID}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Andrei Varabyeu",
                "Plugin-Class": "com.epam.reportportal.extension.bugtracking.jira.JiraStrategyPlugin",
                "Plugin-Service": "api"
//                "Plugin-Dependencies": ""
        )
    }
}

shadowJar {
    configurations = [project.configurations.runtime]
    zip64 true
    dependencies {
        exclude(dependency('com.github.reportportal:'))
    }
}

task plugin(type: Jar) {
    getArchiveBaseName().set("plugin-${pluginID}")
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}
